buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:+'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:+'
        classpath 'com.ofg:uptodate-gradle-plugin:+'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'uptodate'
apply plugin: 'spring-boot'

sourceCompatibility = 1.7

group = 'com.ofg'
description = 'Microservice dependency manager'
version = '0.5.2-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

ext {
    groovyVersion = '2.3.6'
    spockVersion = '0.7-groovy-2.0'
    curatorVersion = '2.6.0'
    undertowVersion = '1.0.15.Final'
}

configurations {
    compile.exclude group: "org.slf4j", module: "slf4j-log4j12"
    compile.exclude group: "org.slf4j", module: "log4j-over-slf4j"
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:$groovyVersion:indy"
    compile "org.apache.curator:curator-x-discovery:$curatorVersion"
    compile "org.apache.curator:curator-test:$curatorVersion"
    compile 'org.jboss.resteasy:resteasy-undertow:3.0.8.Final'
    compile "io.undertow:undertow-core:$undertowVersion"
    compile "io.undertow:undertow-servlet:$undertowVersion"
    compile 'org.apache.ivy:ivy:2.3.0'
    compile 'args4j:args4j:2.0.29'
    compile 'com.google.guava:guava:17.0'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'

    testCompile("org.spockframework:spock-core:$spockVersion") {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }

    testCompile ('com.jayway.awaitility:awaitility-groovy:1.6.1') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

bintrayUpload.dependsOn 'build'

bintray {
    user = System.properties['bintrayUser']
    key = System.properties['bintrayKey']

    publications = ['mavenJava']
    pkg {
        repo = 'micro-deps'
        userOrg = '4finance'
        name = 'micro-deps'
        desc = 'Library for managing microservice dependencies using Apache Curator'
        licenses = ['Apache-2.0']
        labels = ['microservice']
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar {
                classifier "sources"
            }
            artifact jar {
                classifier "fatJar"
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

springBoot {
    classifier = 'fatJar'
    backupSource = false
}

project.tasks.findAll {it.name.startsWith 'publish'}*.dependsOn(bootRepackage)