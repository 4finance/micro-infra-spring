buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.1.8.RELEASE'
        classpath 'com.ofg:uptodate-gradle-plugin:+'
        classpath "com.ofg:micro-common-release:0.1.12"
        classpath "io.spring.gradle:dependency-management-plugin:0.2.1.RELEASE"
        if (project.hasProperty("coverage")) { classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:+' }
        if (project.hasProperty("compatibility")) { classpath "be.insaneprogramming.gradle:animalsniffer-gradle-plugin:+" }
    }
    dependencies {
        ant.unjar src: configurations.classpath.find { it.name.startsWith("micro-common-release") }, dest: 'build/release'
    }
}

apply from: "${rootProject.buildDir}/release/gradle/version.gradle"

scmVersion {
    tag { prefix = '' }
}

allprojects {
    group = 'com.ofg'
    project.version = scmVersion.version
}

Set emptySubprojects = [project(':swagger'), project(':micro-deps'), project(':stub-runner')]
Set srcSubprojects = subprojects - emptySubprojects

configure(srcSubprojects) {
    apply plugin: 'groovy'
    apply from: "${rootProject.buildDir}/release/gradle/publish.gradle"

    bintray {
        pkg {
            repo = project.name.startsWith("micro-deps") ? 'micro-deps' : 'micro'
        }
    }
}

apply from: "${rootProject.buildDir}/release/gradle/release.gradle"

//NOTE: All release and publishing modifications (including further scmVersion closure) should be put AFTER the apply of publish.gradle and/or release.gradle
//      (to be not overridden by the default values applied in those files)


allprojects {
    repositories {
        mavenLocal()
        jcenter()
    }

    configurations {
        all {
            resolutionStrategy {
                exclude group: 'log4j', module: 'log4j'
                force 'org.apache.httpcomponents:httpclient:4.3.+'
                force 'org.jboss.logging:jboss-logging:3.1.4.GA'

                eachDependency { DependencyResolveDetails details ->
                    if (details.requested.group == 'org.slf4j') { details.useVersion '1.7.9' }
                    if (details.requested.group == 'com.fasterxml.jackson.core') { details.useVersion "2.4.4" }

                    // To prevent an accidental usage of groovy-all.jar and groovy.jar in different versions
                    // all modularized Groovy jars are replaced with groovy-all.jar by default.
                    if (details.requested.group == 'org.codehaus.groovy' && details.requested.name != "groovy-all") {
                        details.useTarget("org.codehaus.groovy:groovy-all:${details.requested.version}")
                    }
                }
            }
        }
    }
}

task addHashFile << {
    String gitCmd = "git log --pretty=format:'%H' -n 1"
    def proc = gitCmd.execute()
    proc.waitFor()
    new File("commitHash.txt").withWriter { it << proc.in.text }
}


configure(emptySubprojects + rootProject) {
    apply plugin: 'base'
}

configure(srcSubprojects) {

    apply plugin: 'com.ofg.uptodate'
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = 1.7

    dependencyManagement {
        dependencies {
            "org.codehaus.groovy:groovy-all" "2.3.7"
            "org.springframework.boot:spring-boot-starter-web" "1.1.8.RELEASE"
            "com.google.guava:guava" "18.0"

            dependencySet(group:'org.springframework', version: '4.1.1.RELEASE') {
                entry "spring-webmvc"
                entry "spring-context"
                entry "spring-test"
                entry "spring-aop"
            }
            dependencySet(group:"org.apache.curator", version: "2.7.0") {
                entry "curator-x-discovery"
                entry "curator-test"
            }
            dependencySet(group:"org.slf4j", version: "1.7.9") {
                entry "slf4j-api"
                entry "log4j-over-slf4j"
            }
            dependencySet(group:"com.fasterxml.jackson.core", version: "2.4.4") {
                entry "jackson-databind"
                entry "jackson-core"
            }

            //test
            "ch.qos.logback:logback-classic" "1.1.2"
            "com.jayway.restassured:rest-assured" "2.4.0"
            "com.github.tomakehurst:wiremock" "1.52"

            dependencySet(group:'org.spockframework', version: '0.7-groovy-2.0') {
                entry "spock-core"
                entry "spock-spring"
            }
            dependencySet(group:'com.jayway.awaitility', version: '1.6.3') {
                entry "awaitility"
                entry "awaitility-groovy"
            }
        }
    }

    test {
        jvmArgs project.gradle.startParameter.systemPropertiesArgs.entrySet().collect{"-D${it.key}=${it.value}"}
        //Workaround for the issue with Java 8u11 and 7u65 - http://www.infoq.com/news/2014/08/Java8-U11-Broke-Tools
        jvmArgs '-noverify'
        testLogging {
            exceptionFormat = 'full'
        }
    }

    if (project.hasProperty("compatibility")) {
        apply plugin: 'be.insaneprogramming.gradle.animalsniffer'

        animalsniffer {
            signature = "org.codehaus.mojo.signature:java17:+@signature"
        }

        afterEvaluate {
            animalSniffer.mustRunAfter 'compileGroovy'
        }
    }

    jar {
        dependsOn addHashFile
        from "commitHash.txt"
    }

    //Dependencies in all subprojects - http://solidsoft.wordpress.com/2014/11/13/gradle-tricks-display-dependencies-for-all-subprojects-in-multi-project-build/
    task allDeps(type: DependencyReportTask) {}
    task allInsight(type: DependencyInsightReportTask) {}
}

if (project.hasProperty("coverage")) {
    configure(srcSubprojects + rootProject) {
        apply plugin: 'jacoco'

        jacoco {
            toolVersion = '0.7.1.201405082137'
        }
    }

    configure(srcSubprojects) {
        jacocoTestReport {
            reports {
                xml.enabled = true // coveralls plugin depends on xml format report
            }
        }

        test {
            jacoco {
                excludes = ['*Configuration']
            }
        }
    }

    apply plugin: 'com.github.kt3k.coveralls'

    coveralls {
        sourceDirs = files(srcSubprojects.sourceSets.main.allSource.srcDirs).files.absolutePath
    }

    task jacocoRootReport(type: JacocoReport) {
        // Gather execution data from all subprojects
        // (change this if you e.g. want to calculate unit test/integration test coverage separately)
        executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

        // Add all relevant sourcesets from the subprojects
        srcSubprojects.each {
            sourceSets it.sourceSets.main
        }

        reports {
            xml.enabled true
            xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        }
    }

    // always run the tests before generating the report
    jacocoRootReport.dependsOn {
        srcSubprojects*.test
        srcSubprojects*.jacocoTestReport
    }
}

wrapper {
    gradleVersion '2.2.1'
}
