apply plugin: 'groovy'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
        classpath 'com.ofg:uptodate-gradle-plugin:1.1.0'
        if (project.hasProperty("coverage")) { classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.0' }
    }
}

ext {
    projectGroup = 'com.ofg'
    projectVersion = '0.2.3-SNAPSHOT'

    spockVersion = '0.7-groovy-2.0'
    springVersion = '4.0.7.RELEASE'
    jacocoVersion = '0.7.1.201405082137'
}

if(project.hasProperty("coverage")) {
    apply plugin: 'jacoco'
    apply plugin: 'com.github.kt3k.coveralls'

    jacoco {
        toolVersion = jacocoVersion
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
            html.enabled = true
        }
    }

    task jacocoReport(type: JacocoReport) {
        // Add execution data from all subprojects
        executionData fileTree(project.rootDir.absolutePath).include("*/build/jacoco/*.exec")
        // Add source classes from all subprojects
        subprojects.each {
            sourceSets it.sourceSets.main
        }
        // Make the aggregate report go in a top-level directory somewhere
        reports {
            html {
                enabled true
                destination "release/reports/jacoco"
            }
            xml {
                enabled true
                destination "release/reports/jacoco"
            }
        }
    }
}

jar.enabled = false

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.ofg.uptodate'

    sourceCompatibility = 1.7
    group = projectGroup
    version = projectVersion
    
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.3.6:indy'
        compile "org.springframework:spring-context:$springVersion"
        compile('com.ofg:micro-deps:0.5.1') {
            exclude group: 'log4j'
            exclude group: "org.slf4j", module: "slf4j-log4j12"
            exclude group: "org.slf4j", module: "log4j-over-slf4j"
        }
        testCompile "org.spockframework:spock-core:$spockVersion"
        testCompile "org.spockframework:spock-spring:$spockVersion"
        testCompile "org.springframework:spring-test:$springVersion"
    }

    bintrayUpload.dependsOn 'build'

    bintray {
        user = System.properties['bintrayUser']
        key = System.properties['bintrayKey']

        publications = ['mavenJava']
        pkg {
            repo = 'micro-deps'
            userOrg = '4finance'
            name = "${project.name}"
            desc = 'Default Spring configuration for Microservice Dependency Manager'
            licenses = ['Apache-2.0']
            labels = ['microservice']
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: groovydoc) {
        classifier = 'javadoc'
        from groovydoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }


    //Workaround for the issue with Java 8u11 and 7u65 - http://www.infoq.com/news/2014/08/Java8-U11-Broke-Tools
    test {
        jvmArgs '-noverify'
        if (project.hasProperty("coverage")) {            
            jacoco {
                enabled = true
            }
        }        
    }

    if(project.hasProperty("coverage")) {
        apply plugin: 'jacoco'
        apply plugin: 'com.github.kt3k.coveralls'

        jacoco {
            toolVersion = jacocoVersion
        }

        jacocoTestReport {
            reports {
                xml.enabled = true // coveralls plugin depends on xml format report
                html.enabled = true
            }
        }

        test {
            ignoreFailures = true
        }
    }


}

project(':micro-deps-spring-config') {
    description = 'Microservice dependency manager - default Spring configuration'
}

project(':micro-deps-spring-test-config') {
    description = 'Microservice dependency manager - Spring test configuration'

    dependencies {        
        compile 'cglib:cglib-nodep:3.1'
        compile 'org.objenesis:objenesis:2.1' // allows mocking of classes without default constructor (together with CGLIB)
        compile 'org.aspectj:aspectjweaver:1.8.2'
        compile "org.spockframework:spock-core:$spockVersion"
        compile "org.spockframework:spock-spring:$spockVersion"
        compile "org.springframework:spring-webmvc:$springVersion"
        compile "org.springframework:spring-test:$springVersion"
        compile 'com.github.tomakehurst:wiremock:1.47'
        
        testCompile project(':micro-deps-spring-config')
        testCompile 'com.jayway.restassured:rest-assured:2.3.3'
    }
}


