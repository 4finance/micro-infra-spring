apply plugin: 'groovy'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
        classpath 'com.ofg:uptodate-gradle-plugin:0.0.5'
    }
}

ext {
    projectSourceCompatibility = 1.7
    projectGroup = 'com.ofg'
    projectVersion = '0.2.1-SNAPSHOT'
    
    spockVersion = '0.7-groovy-2.0'
    springVersion = '4.0.6.RELEASE'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'bintray'
    apply plugin: 'uptodate'

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.3.6:indy'
        compile "org.springframework:spring-context:$springVersion"
        compile('com.ofg:micro-deps:0.5.0') {
            exclude group: 'log4j'
            exclude group: "org.slf4j", module: "slf4j-log4j12"
            exclude group: "org.slf4j", module: "log4j-over-slf4j"
        }
        testCompile "org.spockframework:spock-core:$spockVersion"
        testCompile "org.spockframework:spock-spring:$spockVersion"
        testCompile "org.springframework:spring-test:$springVersion"
    }


    sourceCompatibility = projectSourceCompatibility
    group = projectGroup
    version = projectVersion

    bintrayUpload.dependsOn 'build'

    bintray {
        user = System.properties['bintrayUser']
        key = System.properties['bintrayKey']

        publications = ['mavenJava']
        pkg {
            repo = 'micro-deps'
            userOrg = '4finance'
            name = "${project.name}"
            desc = 'Default Spring configuration for Microservice Dependency Manager'
            licenses = ['Apache-2.0']
            labels = ['microservice']
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '2.0'
    }
}

project(':micro-deps-spring-config') {
    description = 'Microservice dependency manager - default Spring configuration'
    
}

project(':micro-deps-spring-test-config') {
    description = 'Microservice dependency manager - Spring test configuration'

    dependencies {        
        compile 'cglib:cglib-nodep:3.1'
        compile 'org.objenesis:objenesis:2.1' // allows mocking of classes without default constructor (together with CGLIB)
        compile 'org.aspectj:aspectjweaver:1.8.1'
        compile "org.spockframework:spock-core:$spockVersion"
        compile "org.spockframework:spock-spring:$spockVersion"
        compile "org.springframework:spring-webmvc:$springVersion"
        compile "org.springframework:spring-test:$springVersion"
        compile 'com.github.tomakehurst:wiremock:1.47'
        
        testCompile project(':micro-deps-spring-config')
        testCompile 'com.jayway.restassured:rest-assured:2.3.2'
    }
}


